<!DOCTYPE html>
<head>
	<meta charset="utf-8" />
    <meta name="author" content="Christina Dickmann">
    <meta name="description" content="Bearbeitung der Einsendeaufgabe 8 im Modul GVT im Master Medieninformatik der HS Emden/Leer.">
    <link rel="stylesheet" href="stylesheetEsa8.css">
    <title>ESA8 - Christina Dickmann</title>

	<script src="ext/gl-matrix.js"></script>
	<script src="GVT_Src_vertexData_plane.js"></script>
	<script src="AntisymmetrischerTorus.js"></script>
	<script src="Tropfen.js"></script>
	<script src="ESA8.js"></script>
	<script type="x-shader/x-vertex" id="vertexshader">
			attribute vec3 aPosition;
			attribute vec3 aNormal;
			
			uniform mat4 uPMatrix;
			uniform mat4 uMVMatrix;
			uniform mat3 uNMatrix;
			
			varying vec3 vNormal;
			varying vec4 vPosition;
			
			void main(){
				// Calculate vertex position in eye coordinates. 
				vec4 tPosition = uMVMatrix * vec4(aPosition, 1.0);
				// Calculate projektion.
				gl_Position = uPMatrix * tPosition;
			
				vec3 tNormal = normalize(uNMatrix * aNormal);
				
				vPosition = tPosition;
				vNormal = tNormal;
			}
		</script>
		<script type="x-shader/x-fragment" id="fragmentshader">
			precision mediump float;
			
			varying vec3 vNormal;
			varying vec4 vPosition;

			// Material.
			struct PhongMaterial {
				vec3 ka;
				vec3 kd;
				vec3 ks;
				float ke;
			};
			uniform PhongMaterial material;

			// Ambient light.
			uniform vec3 ambientLight;

			// Pointlights.
			const int MAX_LIGHT_SOURCES = 8;
			struct LightSource {
				bool isOn;
				vec3 position;
				vec3 color;
			};
			uniform LightSource light[MAX_LIGHT_SOURCES];

			// Phong illumination for single light source,
			// no ambient light.
			vec3 phong(vec3 p, vec3 n, vec3 v, LightSource l){

				vec3 L = l.color;

				vec3 s = normalize(l.position - p);
				vec3 r = reflect(-s, n);

				float sn = max(dot(s,n), 0.0);
				float rv = max(dot(r,v), 0.0);

				vec3 diffuse = material.kd * L * sn;
				vec3 specular = material.ks * L * pow(rv, material.ke);

				return diffuse + specular;
			}

			// Phong illumination for multiple light sources
			vec3 phong(vec3 p, vec3 n, vec3 v) {

				//Calculate ambient light.
				vec3 result = material.ka * ambientLight;

				//Add light from all light sources.
				for(int j=0; j < MAX_LIGHT_SOURCES; j++) {
					if (light[j].isOn){
						result += phong(p, n, v, light[j]);
					}
				}
				return result;
			}
			
			void main() {
				//Calculate view vector.
				vec3 v = normalize(-vPosition.xyz);
				vec3 vNormal = normalize(vNormal);

				gl_FragColor = vec4(phong(vPosition.xyz, vNormal, v), 1.0);
			}
		</script>
</head>
<body onload="app.start()">
	<h1>Christina Dickmann - ESA 8</h1>
	<h2><a href="ESA8_Toon.html">Toon Shader</a></h2>
	<canvas id="canvas" width="1000" height="600"></canvas>
	<div>
	<strong>Taste L:</strong> Starten/Stoppen der Licht-Animation</br>
	<strong>Pfeiltasten links/rechts:</strong> Bewegen der Kamera auf einer Kreisbahn um die y-Achse</br>
	<strong>Pfeiltasten oben/unten:</strong> Bewegen der Kamera um die x-Achse</br>
	<strong>Taste R/Shift+R:</strong> Radius der Kreisbahn einstellen</br>
	<strong>Tasten W/S:</strong> Bewegen der Kamera relativ zur y-Achse</br>
	<strong>Tasten A/D:</strong> Bewegen der Kamera relativ zur x-Achse</br>
	<strong>Taste O/F/P:</strong> Umschalten der Ansicht von orthogonal auf Frustum auf perspektivisch</br>
	<strong>Taste V/Shift+V:</strong> Field of View in perspektivischer Ansicht einstellen</br>
	<strong>Taste B/Shift+B:</strong> Near Plane bei Frustum einstellen</br>
	</div>
</body>